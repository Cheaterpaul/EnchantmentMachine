apply plugin: 'maven'

curseforge {
    if (project.hasProperty("CURSEFORGE_API")) {
        apiKey = project.CURSEFORGE_API
    } else {
        apiKey = "DUMMY"
    }
    project {
        id = "436934"

        if (project.type == "beta") {
            releaseType = 'beta'
            //changelog = new File("resources/changelog/${project.version}.txt").text
        } else if (project.type == "release") {
            releaseType = 'release'
        } else {
            releaseType = 'alpha'
            changelog = "This version should not be used in your 'productive' world. It contains the latest features, but also the latest bugs.\n Probably not all features are finished.\n To see what might have changed browse the latest commits."
        }
        addGameVersion project.minecraft_version

        mainArtifact(jar) {
            displayName = "EnchantmentMachine-${project.minecraft_version}-${project.mod_version}"
        }

        relations {
            optionalDependency 'jei'
        }

    }


}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.9"
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        if (project.hasProperty("MAVEN_URL")) {
            logger.info('Publishing to files server')
            repository(url: project.MAVEN_URL) {
                authentication(userName: project.MAVEN_USER, password: project.MAVEN_TOKEN)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('../../../.m2/repository').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'An Minecraft mod that make enchanting easy'
            url 'https://cheaterpaul.de/projects/enchantmentmachine'

            scm {
                url 'https://github.com/Cheaterpaul/Enchantmentmachine'
                connection 'scm:git:git://github.com/Cheaterpaul/Enchantmentmachine.git'
                developerConnection 'scm:git:git@github.com:Cheaterpaul/Enchantmentmachine.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/Cheaterpaul/Enchantmentmachine/issues'
            }

            developers {
                developer {
                    id 'cheaterpaul'
                    name 'cheaterpaul'
                    roles { role 'developer' }
                }
            }
        }


    }
}