/**
 * copied and modified from https://github.com/MinecraftModDevelopment/Gradle-Collection/blob/eba777ee097440f8eac2d7fd81907c65ce66d9de/minecraft/maven_nomcp.gradle with license LGPL-2.1
 */

apply plugin: 'maven-publish'
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {
                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    // Check if the dependency has been mapped with MCP
                    if (dep.version.last().value().last().contains('_mapped_snapshot_') || dep.version.last().value().last().contains('_mapped_stable_') || dep.version.last().value().last().contains('_mapped_official_')) {

                        // Remove the MCP mapped artifact from the dependency list.
                        println 'Surpressing MCP mapped artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                        assert dep.parent().remove(dep)
                    }
                }
            }

            pom {
                description = 'An Minecraft mod that make enchanting easy'
                scm {
                    url = 'https://github.com/Cheaterpaul/Enchantmentmachine'
                    connection = 'scm:git:git://github.com/Cheaterpaul/Enchantmentmachine.git'
                    developerConnection = 'scm:git:git@github.com:Cheaterpaul/Enchantmentmachine.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/Cheaterpaul/Enchantmentmachine/issues'
                }

                developers {
                    developer {
                        id ='cheaterpaul'
                        name ='cheaterpaul'
                        url = 'paube.de'
                        email = 'dev@paube.de'
                        roles = ["developer"]
                    }
                    developer {
                        id = 'maxanier'
                        name = 'maxanier'
                        roles = ["developer"]
                    }
                }
            }
            // Adds the sources as an artifact.
            artifact sourcesJar {
                classifier 'sources'
            }

            // Adds the javadocs as an artifact.
            artifact javadocJar {
                classifier 'javadoc'
            }

            // Adds the deobfuscated jar as an artifact.
            artifact deobfJar {
                classifier 'deobf'
            }

//            artifact forgelibJar {
//                classifier 'forgelib'
//            }
        }
    }

    repositories {
        maven {
            // Sets the login credentials for your maven.
            credentials {
                username findProperty('MAVEN_USER')
                password findProperty('MAVEN_TOKEN')
            }

            // Sets the maven URL
            url findProperty('MAVEN_URL')
        }
    }
}